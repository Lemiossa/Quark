; bootsect.asm
; Created by Matheus Leme Da Silva
BITS 16
ORG 0x7C00

%DEFINE ADDRESS 0x8000
%DEFINE START_SECTOR 1
%DEFINE SECTOR_COUNT 62

%DEFINE LSEGMENT (ADDRESS >> 4)
%DEFINE LOFFSET (ADDRESS & 0xF)

_START:
	CLI
	XOR AX, AX
	MOV DS, AX
	MOV ES, AX
	MOV SS, AX
	MOV SP, 0x7C00
	STI

	MOV [BOOT_DRIVE], DL

	; Get Disk Geometry
	XOR DI, DI
	MOV AH, 0x08
	INT 0x13

	JC ERROR

	INC DH ; HPC = Heads + 1
	MOV [HPC], DH

	AND CL, 0x3F ; SPT
	MOV [SPT], CL

	MOV EDI, ADDRESS
	MOV EAX, START_SECTOR
LOAD_LOOP:
	CMP EAX, SECTOR_COUNT
	JG JUMP

	CALL DISK_READ_SECTOR

	PUSH AX
	MOV AH, 0x0E
	MOV AL, '.'
	INT 0x10
	POP AX

	INC EAX
	ADD EDI, 512
	JMP LOAD_LOOP

JUMP:
	MOV AH, 0xE
	MOV AL, 0xA
	INT 0x10
	MOV AL, 0xD
	INT 0x10

	MOV DL, [BOOT_DRIVE]
	MOV EBX, [DISK_IDENTIFIER]
	JMP LSEGMENT:LOFFSET
	JMP HANG

; Handles error and halts the system
ERROR:
	MOV SI, ERROR_MSG
	CALL PRINT_STRING
HANG:
	JMP HANG

; Read sector in EAX at EDI address
DISK_READ_SECTOR:
	PUSH EAX
	PUSH EBX
	PUSH ECX
	PUSH EDX
	PUSH EDI

	; LBA To CHS
	XOR EBX, EBX
	MOV BL, [SPT]
	XOR EDX, EDX
	DIV EBX
	INC EDX
	MOV CL, DL ; Sector

	XOR EDX, EDX
	XOR EBX, EBX
	MOV BL, [HPC]
	DIV EBX
	MOV DH, CL
	MOV BL, DL
	MOV DL, DH
	MOV DH, BL
	; DL: Sector
	; DH: Head
	; AX: Cylinder

	PUSH BX
	PUSH ES

	; Linear addr to SEG:OFF
	PUSH EDI
	SHR EDI, 4
	MOV ES, DI
	POP EDI

	AND EDI, 0xF
	MOV BX, DI

	; Read
	MOV CH, AL
	MOV CL, AH
	SHL CL, 6
	AND CL, 0xC0
	OR CL, DL

	MOV DL, [BOOT_DRIVE]
	MOV AX, 0x0201
	INT 0x13

	POP ES
	POP BX

	JC ERROR

	POP EDI
	POP EDX
	POP ECX
	POP EBX
	POP EAX
	RET

; Prints a string in DS:SI while no \0
PRINT_STRING:
	PUSH AX
	PUSH SI
	.LOOP:
		LODSB
		CMP AL, 0
		JE .EXIT
		MOV AH, 0xE
		INT 0x10
		JMP .LOOP
	.EXIT:
		POP SI
		POP AX
		RET

ERROR_MSG: DB "ERROR! Failed to load stage 2",0
BOOT_DRIVE: DB 0
HPC: DB 0
SPT: DB 0

TIMES 440 - ($ - $$) DB 0
DISK_IDENTIFIER: DD 0x00000000
TIMES 510 - ($ - $$) DB 0
DW 0xAA55

